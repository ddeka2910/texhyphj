// For bnd
buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:4.1.0'
	}
}

plugins {
	id 'java'
	id 'maven-publish'
	id 'signing'
}

apply plugin: 'biz.aQute.bnd.builder'

group = "com.googlecode.texhyphj"

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc {
	options.encoding = 'UTF-8'
	options.source = 8
	options.links('https://docs.oracle.com/javase/8/docs/api/');
}

def repoRevision = System.getenv("REPO_REVISION")!=null?System.getenv("REPO_REVISION"):repositoryRevision

repositories {
	mavenCentral()
	mavenLocal()
}

dependencies {
	testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
	testImplementation group: 'junit', name: 'junit', version: '4.7'
}

jar {
	manifest {
		attributes(
			// See https://github.com/bndtools/bnd/issues/2677
			'-exportcontents': 'net.davidashen.*',
			'Bundle-Name': "$bundleName",
			'Bundle-SymbolicName': "$bundleName",
			'Built-By': System.getProperty("user.name"),
			'Built-On': new Date().format('yyyy-MM-dd'),
			'Implementation-Title': "$releaseName",
			'Repository-Revision': "$repositoryRevision",
			'Repository-URL': "$repositoryURL",
		)
	}
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

signing {
	required { isReleaseVersion }
	sign publishing.publications
}

publishing {
	//Only upload if a git hash is supplied. On Travis only upload snapshots.
	//enabled = repoRevision.size()==40 && System.getenv("TRAVIS_BUILD_ID")!=null
	publications {
		maven(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			/*
			if (isReleaseVersion) {
				beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
			}*/
			pom {
				name = 'texhyphj'
				packaging = 'jar'
				description = 'TeX Hyphenator in Java.'
				url = "$repositoryURL"
				scm {
					connection = "$repositorySCM"
					developerConnection = "$repositorySCM"
					url = "$repositoryURL"
				}
				licenses {
					license {
						name = 'LGPL'
						url = 'http://www.gnu.org/licenses/lgpl.html'
					}
				}
				developers {
					developer {
						name = 'David Tolpin'
					}
					developer {
						id = 'joel'
						name = 'Joel HÃ¥kansson'
					}
					developer {
						name = 'Jakob Eklund'
					}
				}
			}
		}
	}
	repositories {
		maven {
			def stagingRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
			def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
			url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : stagingRepoUrl
			credentials {
				username = sonatypeUsername
				password = sonatypePassword
			}
		}
	}
}

task zip(type: Zip, dependsOn: [jar, javadoc]) {
	into ("$releaseName") {
		from (jar.archivePath)
		into ('doc') {
			from ('doc')
			into ('api') {
				from javadoc.destinationDir
			}
		}
		into ('etc') { 
			from('build-resources/etc')
		}
		from ('src/main/java')
	}
}

wrapper {
	distributionType = Wrapper.DistributionType.ALL
}